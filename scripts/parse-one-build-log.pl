#!/usr/bin/env perl
use 5.14.0;
use warnings;
#use Data::Dump ( qw| dd pp| );
use Carp;
use CPAN::cpanminus::reporter::RetainReports;
use Getopt::Long;

=head1 NAME

parse-one-build-log.pl - Parse F<cpanm> F<build.log> into JSON files

=head1 USAGE

Typical:

    perl parse-one-build-log.pl  \
        --log=/home/jkeenan/.cpanm/work/1558106887.67500/build.log \
        --analysisdir=/home/jkeenan/learn/perl/p5p/5.30.0-RC2
        --verbose

Complete:

    perl parse-one-build-log.pl  \
        --log=/home/jkeenan/.cpanm/work/1558106887.67500/build.log \
        --analysisdir=/home/jkeenan/learn/perl/p5p/5.30.0-RC2 \
        --verbose \
        --cpanmdir=/home/jkeenan/.cpanm \
        --cpanreporterdir=/home/jkeenan/.cpanreporter

=head1 DESCRIPTION

This program presumes that the user has (i) built a F<perl> from source, (ii)
installed F<cpanm> against that F<perl>, and (iii) installed a set of CPAN
distributions against that F<perl> using F<cpanm> as the installer.

The user wants to parse the F<build.log> file generated by F<cpanm> to write
F<.json> files for each of the CPAN distributions in the list.

The user must specify values on the command-line for C<log> -- the
F<build.log> files (not compressed) -- and C<analysisdir> -- the directory
into which those F<.json> files will be written.

=head1 PREREQUISITES

=over 4

=item * F<perl> 5.14.0 or higher (core distribution used: F<Carp>, F<Getopt::Long>)

=item * F<cpanm> utility installed against that F<perl>

=item * CPAN distributions: F<CPAN::cpanminus::reporter::RetainReports>

=item * Environment variable: C<$HOMEDIR>

=back

=cut

my ($cpanmdir, $cpanreporterdir, $analysisdir, $log, $verbose) = ("") x 4;

#/home/jkeenan/learn/perl/p5p/5.30.0-RC2
GetOptions(
	"cpanmdir=s"	    => \$cpanmdir,
	"cpanreporterdir=s"	=> \$cpanreporterdir,
	"analysisdir=s"	    => \$analysisdir,
	"log=s"	            => \$log,       # note: not gzipped
    "verbose"           => \$verbose
) or croak("Error in command line arguments");

$cpanmdir ||= "$ENV{HOMEDIR}/.cpanm";
$cpanreporterdir ||= "$ENV{HOMEDIR}/.cpanreporter";
local $ENV{PERL_CPANM_HOME}         = $cpanmdir;
local $ENV{PERL_CPAN_REPORTER_DIR}  = $cpanreporterdir;

for my $d ($cpanmdir, $cpanreporterdir, $analysisdir) {
    croak "Could not locate directory $d" unless -d $d;
}
croak "Could not locate cpanm build log $log" unless -f $log;

if ($verbose) {
    say "cpanm build.log:    $log";
    say "analysis directory: $analysisdir";
}

my $reporter = CPAN::cpanminus::reporter::RetainReports->new(
    force               => 1,
    build_dir           => $cpanmdir,
    build_logfile       => $log,
    'ignore-versions' => 1,
);

$reporter->set_report_dir($analysisdir);
$reporter->run;

say "Finished";
__END__

